#!/usr/bin/perl
package GmailNotifier;
use Moose;
use AnyEvent;
use AnyEvent::Gmail::Feed;

with 'MooseX::Getopt';

has 'username' => (is => 'rw', isa => 'Str', required => 1);
has 'password' => (is => 'rw', isa => 'Str', required => 1);
has 'interval' => (is => 'rw', isa => 'Int', default  => 60);

sub run {
    my ($self, %args) = @_;

    my %seen;
    AnyEvent::Gmail::Feed->new(
        username => $self->username,
        password => $self->password,
        interval => $self->interval,
        on_feed => sub {
            my $feed = shift;
            for my $e ($feed->entries) {
                unless ($seen{$e->id}){
                    ## you can send notify with Growl, Notify-OSD or whatever
                    warn "send notify: " . $e->summary;
                }
                $seen{$e->id}++;
            }
        },
    );
    AnyEvent->condvar->recv;
}

package main;
GmailNotifier->new_with_options->run;
